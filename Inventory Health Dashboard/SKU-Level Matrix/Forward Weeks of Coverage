#DAX Code:

(SKU-Level Matrix) Forward Weeks of Coverage = 
VAR Inventory = SUM('Consolidated On-Hand Inventory'[On-Hand Quantity]) -- Starting inventory
VAR CurrentWeekStart = (TODAY() - WEEKDAY(TODAY(), 2) + 1) -- Start of the current week (Sunday)
VAR FutureWeeks =
    FILTER(
        ALL('Rolling Calendar'),
        'Rolling Calendar'[Start of Week] > CurrentWeekStart -- Exclude the current week
    ) -- All future weeks
VAR CumulativeDemand =
    ADDCOLUMNS(
        FutureWeeks,
        "@CumulativeDemand",
        SUMX(
            FILTER(
                ALL('Rolling Calendar'),
                'Rolling Calendar'[Start of Week] <= EARLIER('Rolling Calendar'[Start of Week]) &&
                'Rolling Calendar'[Start of Week] > CurrentWeekStart -- Exclude the current week
            ),
            CALCULATE(SUM('Consolidated Demand Forecast'[Demand Quantity]))
        )
    ) -- Calculate cumulative demand per week, excluding the current week
VAR DepletionWeeks =
    ADDCOLUMNS(
        CumulativeDemand,
        "@RemainingInventory",
        Inventory - [@CumulativeDemand] -- Remaining inventory after subtracting cumulative demand
    )
VAR WeeksToZero =
    COUNTROWS(
        FILTER(
            DepletionWeeks,
            [@RemainingInventory] > 0 -- Count weeks where inventory remains positive
        )
    )
VAR Result =
    IF(
        Inventory <= 0, 
        0, -- If the starting inventory is already 0 or negative, FWC is 0
        IF(
            ISBLANK(WeeksToZero) || WeeksToZero = 0, 
            0, -- If inventory depletes in the first future week or no positive weeks remain
            WeeksToZero
        )
    )
RETURN
    Result

#Explanation:

VAR Inventory is set equal to the total On-Hand Inventory quantity. The reason it uses this instead of Running 
Inventory is to highlight SKUs that need their inventory replenishment expedited to meet demand in the short-term.

VAR CurrentWeekStart determines the start date of the current week, which is Sunday for the company. TODAY() gets 
today's date, WEEKDAY(TODAY(), 2) adjusts for Sunday as the first day of the week.

VAR FutureWeeks retrieves all future weeks (excluding the current week) from the Rolling Calendar table.

VAR CumulativeDemand calculates cumulative demand for each future week. The EARLIER() function ensures that we
sum up demand from all past weeks up to the current row's week within the filter context of the matrix.

VAR DepletionWeeks determines how much inventory remains after subtracting cumulative demand for each future week.

VAR WeeksToZero counts how many future weeks still have positive inventory.

VAR Result returns the number of weeks before inventory reaches zero.

#Used in:

- Top-Risk SKUs Matrix
